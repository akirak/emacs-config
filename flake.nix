{
  inputs = {
    utils.url = "github:numtide/flake-utils";
    flake-utils-plus.url = "github:gytis-ivaskevicius/flake-utils-plus/v1.3.1";

    home-manager.url = "github:nix-community/home-manager";

    # Emacs
    emacs-overlay.url = "github:nix-community/emacs-overlay";
    org-babel.url = "github:akirak/nix-org-babel";
    twist.url = "github:akirak/emacs-twist/devel";
    emacs-inventories.url = "path:./emacs/inventories";

    # pre-commit
    pre-commit-hooks = {
      url = "github:cachix/pre-commit-hooks.nix";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.flake-utils.follows = "utils";
    };
    flake-no-path = {
      url = "github:akirak/flake-no-path";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.flake-utils.follows = "utils";
      inputs.pre-commit-hooks.follows = "pre-commit-hooks";
    };
  };

  outputs =
    { self
    , nixpkgs
    , flake-utils-plus
    , utils
    , home-manager
    , ...
    } @ inputs:
    let
      inherit (builtins) removeAttrs;
      mkApp = utils.lib.mkApp;
      # pkgs = self.pkgs.x86_64-linux.nixpkgs;
    in
    flake-utils-plus.lib.mkFlake {
      inherit self inputs;

      supportedSystems = [ "x86_64-linux" ];

      channelsConfig = {
        allowBroken = false;
      };

      sharedOverlays = [
        inputs.emacs-overlay.overlay
        inputs.org-babel.overlay
        inputs.twist.overlay
        (import ./emacs/overlay.nix {
          inherit (inputs) twist org-babel;
          inherit (inputs.emacs-inventories.lib) inventories;
        })
      ];

      # Nixpkgs flake reference to be used in the configuration.
      # Autogenerated from `inputs` by default.
      # channels.<name> = {}

      hostsDefaults = {
        system = "x86_64-linux";

        # Default modules to be passed to all hosts.
        modules = [
          ./nixos/modules/defaults.nix
        ];

        # channelName = "unstable";
      };

      #############
      ### hosts ###
      #############

      hosts.container = {
        extraArgs = {
          home = import ./hm/home.nix;
        };

        modules =
          [
            {
              boot.isContainer = true;
              networking.useDHCP = false;
              networking.firewall.allowedTCPPorts = [ ];

              services.openssh = {
                enable = true;
              };
            }
            home-manager.nixosModules.home-manager
            ./nixos/modules/default-user.nix
          ];
      };

      #############################
      ### flake outputs builder ###
      #############################

      outputsBuilder = channels:
        let
          inherit (channels.nixpkgs) system;
          inherit (channels.nixpkgs) emacsConfigurations;
          emacs-full = emacsConfigurations.full;
          emacs-basic = emacsConfigurations.basic;
          emacs-compat = emacsConfigurations.compat;

          emacsSandbox = channels.nixpkgs.callPackage ./sandbox/emacs.nix { };

          useDoomTheme = themeName: [
            "--eval"
            "(when init-file-user (require 'doom-themes) (load-theme '${themeName} t))"
          ];
        in
        {
          packages = {
            inherit emacs-full;
            # Add more variants of the full profile later
            emacs = emacsSandbox emacs-basic {
              emacsArguments = useDoomTheme "doom-tomorrow-night";
            };
            emacs-compat = emacsSandbox emacs-compat {
              # emacsArguments = useDoomTheme "doom-tomorrow-night";
            };
            emacs-beancount = emacsSandbox emacsConfigurations.beancount {
              emacsArguments = useDoomTheme "doom-opera-light";
              extraBubblewrapOptions = [
                "--bind"
                "$HOME/beancount"
                "$HOME/beancount"
                "--bind-try"
                "$HOME/Downloads"
                "$HOME/Downloads"
              ];
            };
          };

          apps =
            {
              lock = mkApp {
                drv = emacs-full.lock.writeToDir "emacs/sources";
              };
              sync = mkApp {
                drv = emacs-full.sync.writeToDir "emacs/sources";
              };
              update = mkApp {
                drv = emacs-full.update.writeToDir "emacs/sources";
              };
            };

          # Set up a pre-commit hook by running `nix develop`.
          devShell = channels.nixpkgs.mkShell {
            inherit (inputs.pre-commit-hooks.lib.${system}.run {
              src = ./.;
              hooks = {
                nixpkgs-fmt.enable = true;
                # nix-linter.enable = true;
                flake-no-path = {
                  enable = true;
                  name = "Ensure that flake.lock does not contain a local path";
                  entry = "${
                    inputs.flake-no-path.packages.${system}.flake-no-path
                  }/bin/flake-no-path";
                  files = "flake\.lock$";
                  pass_filenames = true;
                };
                emacs-config = {
                  enable = true;
                  name = "Update the documentation of the Emacs configuration";
                  stages = [ "push" ];
                  entry = "${
                    (channels.nixpkgs.emacsPackagesFor emacs-full.emacs).emacsWithPackages
                      (epkgs: [ epkgs.org-make-toc ])
                  }/bin/emacs --batch -l ${./scripts/update-emacs-config.el}
 -f akirak/batch-update-emacs-config";
                  files = "emacs-config\.org$";
                  pass_filenames = true;
                };
              };
            }) shellHook;
          };
        };

      #########################################################
      ### All other properties are passed down to the flake ###
      #########################################################

      # checks.x86_64-linux.someCheck = pkgs.hello;
      # packages.x86_64-linux.somePackage = pkgs.hello;
      # overlay = import ./overlays;
      # abc = 132;
    };
}
